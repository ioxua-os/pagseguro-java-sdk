group 'br.com.uol.pagseguro'
version '4.1.1'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile(group: 'junit', name: 'junit', version: '4.13')
    testCompile(group: 'org.mockito', name: 'mockito-core', version: '2.28.2')
    testCompile(group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.7')
    testCompile(group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.5')
    testCompile(group: 'org.javassist', name: 'javassist', version: '3.24.1-GA')
    testCompile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
    testCompile(group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.3')
    testCompile(group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.3')
    testCompile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2')
    testCompile(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.2')

    compileOnly(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
    compileOnly(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21')
    compileOnly(group: 'commons-logging', name: 'commons-logging', version: '1.2')
    compileOnly(group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.3')
    compileOnly(group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.3')
    compileOnly(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2')
    compileOnly(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.2')
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.name
                packaging 'jar'

                description 'A collection of domain classes to ease integrations with PagSeguro Api'
                url 'https://github.com/pagseguro/pagseguro-java-sdk/'

                scm {
                    url 'https://github.com/pagseguro/pagseguro-java-sdk/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'pagseguro'
                        name 'PagSeguro'
                        email 'desenvolvedores@pagseguro.com.br'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Uncomment this block to sign the files when building to upload to Maven
//signing {
//    sign configurations.archives
//}

